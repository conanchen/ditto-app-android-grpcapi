apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.google.protobuf'

group = "io.github.conanchen"
archivesBaseName = "ditto-app-android-grpcapi"
version = "1.0.0"
sourceCompatibility = 1.6
targetCompatibility = 1.6

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment);
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'ditto'
                packaging 'jar'
                description '项目开源地址'
                url 'https://github.com/conanchen/ditto-app-android-grpcapi'

                scm {
                    connection 'scm:git:https://github.com/conanchen/ditto-app-android-grpcapi.git'
                    developerConnection 'scm:git:https://github.com/conanchen/ditto-app-android-grpcapi.git'
                    url 'https://github.com/conanchen/ditto-app-android-grpcapi.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'ditto'
                        name 'conan chan'
                        email 'conan8chan@yahoo.com'
                    }
                }
            }
        }
    }
}
tasks.withType(JavaCompile){
    options.encoding = "UTF-8"
}
dependencies {
    compile 'io.grpc:grpc-all:1.0.0'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.0.0"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.0.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

//参见Part 2， 为项目生成**.jar/**-javadoc.jar/**-sources.jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

//为所有的jar包做数字签名
signing {
    sign configurations.archives
}
